import org.jetbrains.grammarkit.tasks.GenerateLexer
import org.jetbrains.grammarkit.tasks.GenerateParser

buildscript {
  repositories {
    mavenCentral()
  }
}

plugins {
  id "org.jetbrains.intellij" version "0.4.18"
  id "org.jetbrains.grammarkit" version "2019.2"
}

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'org.jetbrains.grammarkit'

intellij {
  // see https://www.jetbrains.com/intellij-repository/releases
  version 'IC-2019.2'
  pluginName 'q'
  // see https://github.com/JetBrains/gradle-intellij-plugin/issues/391
  buildSearchableOptions.enabled = false
}

version '1.26' // Plugin version
patchPluginXml {
  untilBuild '202.*'
}

grammarKit {
  jflexRelease = '1.7.0-1'
}

sourceSets {
  main {
    java {
      srcDir 'gen'
    }
    resources {
      srcDir 'src/main/resources'
    }
  }
}

task generateKLexer(type: GenerateLexer) {
  source = "src/main/resources/com/appian/intellij/k/k.flex"
  targetDir = "gen/com/appian/intellij/k"
  targetClass = "KLexer"
  purgeOldFiles = true
}

task generateKParser(type: GenerateParser) {
  source = "src/main/resources/com/appian/intellij/k/k.bnf"
  targetRoot = 'gen'
  pathToParser = 'com/appian/intellij/k/parser/KParser.java'
  pathToPsiRoot = 'com/appian/intellij/k/psi'
  purgeOldFiles = true
}

compileJava {
  dependsOn generateKLexer
  dependsOn generateKParser
}

if (project.hasProperty('intellijPublishToken')) {
  publishPlugin {
    token intellijPublishToken
  }
}
