import org.jetbrains.grammarkit.tasks.GenerateLexer
import org.jetbrains.grammarkit.tasks.GenerateParser

buildscript {
  repositories {
    mavenCentral()
  }
}

plugins {
  id "org.jetbrains.intellij" version "0.4.7"
  id "org.jetbrains.grammarkit" version "2018.3.1"
}

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'org.jetbrains.grammarkit'
apply plugin: 'java'

intellij {
  // see https://www.jetbrains.com/intellij-repository/releases
  version 'IC-2018.1.8'
  pluginName 'q'
}

version '1.14' // Plugin version
patchPluginXml {
  untilBuild '191.*'
}

grammarKit {
  jflexRelease = '1.7.0-1'
}

sourceSets {
  main {
    java {
      srcDir 'gen'
    }
    resources {
      srcDir 'src/main/resources'
    }
  }
}

task generateKLexer(type: GenerateLexer) {
  source = "src/main/resources/com/appian/intellij/k/k.flex"
  targetDir = "gen/com/appian/intellij/k"
  targetClass = "KLexer"
  purgeOldFiles = true
}

task generateKParser(type: GenerateParser) {
  source = "src/main/resources/com/appian/intellij/k/k.bnf"
  targetRoot = 'gen'
  pathToParser = 'com/appian/intellij/k/parser/KParser.java'
  pathToPsiRoot = 'com/appian/intellij/k/psi'
  purgeOldFiles = true
}

compileJava {
  dependsOn generateKLexer
  dependsOn generateKParser
}

if (project.hasProperty('intellijPublishUsername')) {
  publishPlugin {
    username intellijPublishUsername
    password intellijPublishPassword
  }
}
